buildscript {
    dependencies {
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
    }
}

plugins {
    id 'eclipse'
    id 'maven-publish'
    id 'net.minecraftforge.gradle' version '6.0.+'
    id 'org.parchmentmc.librarian.forgegradle' version '1.+'
    id 'org.spongepowered.mixin' version '0.7.+'
}

repositories {
    maven {
        url 'https://cursemaven.com'
    }
    mavenCentral()
    maven { url "https://maven.blamejared.com/" }
    maven { url "https://modmaven.dev" }
}

group = 'net.abraxator.moresnifferflowers'
archivesBaseName = 'MoreSnifferFlowers-'+mc_version+'-forge'

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

println "Java: ${System.getProperty 'java.version'}, JVM: ${System.getProperty 'java.vm.version'} (${System.getProperty 'java.vendor'}), Arch: ${System.getProperty 'os.arch'}"
minecraft {
    //mappings channel: 'official', version: '1.20.1'
    mappings channel: 'parchment', version: '2023.09.03-1.20.1'

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        configureEach {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'forge.enabledGameTestNamespaces', 'moresnifferflowers'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
            mods {
                more_sniffer_flowers {
                    source sourceSets.main
                }
            }
        }

        client {}
        server {}
        gameTestServer {}

        data {
            workingDirectory project.file('run')
            args '--mod', 'moresnifferflowers', '--all',
                    '--output', file('src/generated/resources/'),
                    '--existing', file('src/main/resources/')
        }
    }
}

// Include resources generated by data generators.
sourceSets.main.resources { srcDir 'src/generated/resources' }

dependencies {
    //jei
    implementation fg.deobf("mezz.jei:jei-${mc_version}-forge:${jei_version}")
    
    //mixin
    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
    
    //quark
    compileOnly("curse.maven:Quark-${quark_id}:${quark_version}")
    
    //right click harvest
    compileOnly("curse.maven:RightClickHarvest-${RCH_id}:${RCH_version}")
    
    //minecraft
    minecraft "net.minecraftforge:forge:${mc_version}-${forge_version}"
}

mixin {
    add sourceSets.main, 'more_sniffer_flowers.mixins.refmap.json'
    config 'more_sniffer_flowers.mixins.json'
    debug.verbose = true
    debug.export = true
}

final manifestAttributes = [
        'Specification-Title'   : 'moresnifferflowers',
        'Specification-Vendor'  : 'AbraXator',
        'Specification-Version' : '1',
        'Implementation-Title'  : project.name,
        'Implementation-Version': project.jar.archiveVersion,
        'Implementation-Vendor' : 'AbraXator'
]

jar {
    manifest.attributes(manifestAttributes)
    finalizedBy('reobfJar')
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}
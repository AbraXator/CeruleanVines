
plugins {
    id 'maven-publish'
    id "org.jetbrains.gradle.plugin.idea-ext" version "1.1.7"
    id 'net.neoforged.moddev' version '2.0.74'
}

group = 'net.abraxator.moresnifferflowers'
archivesBaseName = 'MoreSnifferFlowers-'+minecraft_version+'-neo'

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        url "https://cursemaven.com"
    }
    maven { url = "https://api.modrinth.com/maven" }
    maven { url = "https://maven.blamejared.com/" }
    maven { url = "https://modmaven.dev" }
    maven {
        name 'Shedaniel\'s Maven' //REI
        url "https://maven.shedaniel.me"
    }
    maven {
        name = "TerraformersMC" //EMI
        url = "https://maven.terraformersmc.com/"
    }
}


java.toolchain.languageVersion = JavaLanguageVersion.of(21)

tasks.withType(JavaCompile).configureEach {
    options.release = 21
}

println "Java: ${System.getProperty 'java.version'}, JVM: ${System.getProperty 'java.vm.version'} (${System.getProperty 'java.vendor'}), Arch: ${System.getProperty 'os.arch'}"

neoForge {
    version = project.neoforge_version
    validateAccessTransformers = true

    runs {
        configureEach {
            gameDirectory = layout.projectDir.dir("runs/$name")
            systemProperty 'forge.logging.console.level', 'debug'
            systemProperty 'forge.logging.markers', 'REGISTRIES'

        }
        client {
            client()
        }
        clientAuth {
            client()
            devLogin = true
            gameDirectory = layout.projectDir.dir("runs/client")
        }
        data {
            data()
            programArguments.addAll '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
        }
        server {
            server()
        }
    }
    mods {
        more_sniffer_flowers {
            sourceSet(sourceSets.main)
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

dependencies {
    //implementation "me.shedaniel:RoughlyEnoughItems-neoforge:${project.rei_version}"
    compileOnly("mezz.jei:jei-${minecraft_version}-common-api:${jei_version}")
    compileOnly("mezz.jei:jei-${minecraft_version}-neoforge-api:${jei_version}")
    runtimeOnly("mezz.jei:jei-${minecraft_version}-neoforge:${jei_version}")
    compileOnly("curse.maven:rightclickharvest-452834:5427523")
    //implementation("me.shedaniel.cloth:cloth-config-neoforge:${cloth_version}")
    //implementation("dev.architectury:architectury-neoforge:${architectury_version}")
    implementation("net.neoforged:neoforge:${neoforge_version}")
}

final manifestAttributes = [
        'Specification-Title'   : 'More Sniffer Flowers',
        'Specification-Vendor'  : 'AbraXator',
        'Specification-Version' : project.version,
        'Implementation-Title'  : project.name,
        'Implementation-Version': project.jar.archiveVersion,
        'Implementation-Vendor' : 'AbraXator'
]

jar {
    manifest.attributes(manifestAttributes)
//    finalizedBy('reobfJar')
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}